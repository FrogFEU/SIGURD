#define CheckVictory(nextLabel, endLabel) "COUNTER_CHECK CounterEnemyOCs; BNE nextLabel sC s0; ENUT FlagDefeatBoss; CALL EndingScene; GOTO endLabel;"

EnemyOCDeathEvent:
ENUF FlagEnemyOCDied
COUNTER_DEC CounterEnemyOCs
CheckVictory(20, 120)
LABEL 20
CALL EOCDeathUpdateMusic
// Remove fog if 6048 is dead
BranchIfAlive(EOC6048, 30)
FOGVISION 0
LABEL 30
CALL EOCDeathAthenaReaction
CALL EOC2136SetupAnotherOCDeath
LABEL 120
NoFade
ENDA

EOC2136SetupAnotherOCDeath:
// Set flag based on killer ID
GetKillerUnitID
MoveMemorySlot(s1, sC)
EOCKillerUnitIDToFlagEventNoArgs
MoveMemorySlot(s2, sC)
ENUT -1
// Store dead unit coords
GetDeathUnitCoord
MoveMemorySlot(s2, sC)
SVAL 1 EOC2136MemoryStorage
EOCDeathStoreCoordsSomewhereDumbEventNoArgs
BranchIfDead(EOC2136, 20)
// Immediate teleport
CALL EOC2136UpdateWeapon
CAMERA EOC2136
CHECK_COORDS EOC2136
MoveMemorySlot(sB, sC)
WARPOUT [-1, -1]
HIDEUNIT EOC2136
ENDWARP
CALL EOC2136UpdatePosition
BranchIfMissingItem(EOC2136, Incorporeal, 120)
BranchIfMissingItem(EOC2136, AvengerPlus, 30)
// Immediate regen
EOCDeathStoreActiveUnitSomewhereDumbEvent(EOC2136MemoryStorage)
CALL EOC2136RestoreFormEvent
EOCDeathRecoverActiveUnitFromSomewhereDumbEventInternal(EOC2136MemoryStorage)
GOTO 120
LABEL 30
// Delayed regen
SilentGiveItem(EOC2136, Regenerating)
GOTO 120
LABEL 20
// Delayed spawn
ENUF FlagEOC2136SpawnBlocker
LABEL 120
NoFade
ENDA

EOC2136DeathEvent:
ENUF FlagEOC2136Died
COUNTER_DEC CounterEnemyOCs
CheckVictory(30, 120)
LABEL 30
//CALL Summon2136Event
LOAD1 0x1 EOC2136UnitPlacement
ENUN
ShallowReclassEvent(EOC2136, EOCCSceptre)
SilentGiveItem(EOC2136, Incorporeal)
HIDEUNIT EOC2136
//CALL EOC2136UpdateWeapon
EOCDeathStoreCoordsSomewhereDumbEventNoArgs
CALL EOC2136UpdatePosition
CHECK_HARD
BEQ 20 sC s0
SilentRemoveItemEvent(EOC2136, Avenger)
SilentGiveItem(EOC2136, AvengerPlus)
LABEL 20
COUNTER_INC CounterEnemyOCs
LABEL 120
NoFade
ENDA

EOC2136UpdateWeapon:
EOCKillerFlagToWeaponEvent
MoveMemorySlot(s2, sC)
SVAL 1 EOC2136
SVAL 3 0
SetItemByIndexEventNoArgs

ENUF FlagEOCKillerValoria
ENUF FlagEOCKillerMorbius
ENUF FlagEOCKillerBill
ENUF FlagEOCKillerSintel
ENUF FlagEOCKillerOther
NoFade
ENDA

EOC2136UpdatePosition:
EOCDeathRecoverCoordsFromSomewhereDumbEvent(EOC2136MemoryStorage)
MoveMemorySlot(s2, sC)
SVAL 1 EOC2136
MoveMemorySlot(sB, sC)
CAMERA_SB
ENUN
WARPIN [-1, -1]
InstantMoveToPositionEventNoArgs
SHOWUNIT EOC2136
ENDWARP
NoFade
ENDA

EOC2136RestoreFormEvent:
SilentRemoveItemEvent(EOC2136, Incorporeal)
SilentRemoveItemEvent(EOC2136, Regenerating)
CAMERA EOC2136
EARTHQUAKE 0 1
SOUN 0x2F5
ShallowReclassEvent(EOC2136, EOCCVengeance)
SVAL 1 EOC2136
ASMC ASMC_HealBadStatusLikeBoon|1 // HealBadStatusLikeBoon()
EARTHQUAKE_END
NoFade
ENDA

EOCDeathUpdateMusic:
COUNTER_CHECK CounterEnemyOCs
SVAL 1 1
BEQ 20 sC s1
SetChapterSong(SongRhapsodyOfTheForsakenID, SongScatteredAndLostID, SongRhapsodyOfTheForsakenID, 1)
GOTO 120
LABEL 20
SetChapterSong(SongVenusLighthouseID, SongCombatAdversityID, SongVenusLighthouseID, 1)
LABEL 120
NoFade
ENDA

EOCDeathAthenaReaction:
BranchIfDead(EOC2136, 40)
SVAL 1 1
SVAL 2 2
SVAL 3 3
COUNTER_CHECK CounterEnemyOCs
BNE 21 sC s1
Text(AthenaTextEOCDeath3)
GOTO 120
LABEL 21
BNE 22 sC s2
Text(AthenaTextEOCDeath2)
GOTO 120
LABEL 22
BNE 23 sC s3
LABEL 40
Text(AthenaTextEOCDeath1)
GOTO 120
LABEL 23
LABEL 120
NoFade
ENDA
