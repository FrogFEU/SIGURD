#define BranchIfMissingItem(unit, item, label) "HasItemEvent(unit, item); BEQ label sC s0;"
#define IncrementItemUsesAndBranchIfNotFull(unit, item, label) "RestoreItemUsesEvent(unit, item, 1); BEQ label sC s0;"
#define EOCTurnEventHeader(unit, item, label) "BranchIfDead(unit, label); BranchIfMissingItem(unit, item, label);"
#define EOCCooldownItemHeader(unit, item, label) "IncrementItemUsesAndBranchIfNotFull(unit, item, label); SetItemUsesEvent(unit, item, 0);"
#define EOC5397Promote(oldID, newID) "CAMERA oldID; SVAL 1 oldID; SVAL 2 newID; CALL Promote5397Event;"
#define EOC5397Phaser(oldID, newID, itemID, nextLabel, endLabel) "EOCTurnEventHeader(oldID, itemID, nextLabel); EOCCooldownItemHeader(oldID, itemID, endLabel); RestoreItemUsesEvent(oldID, MasterworkProgress, 1); EOC5397Promote(oldID, newID); GOTO endLabel; LABEL nextLabel;"

BeginTurn3811:
BranchIfDead(EOC3811, 120)
BranchIfMissingItem(EOC3811, Dollcraft, 20)
EOCCooldownItemHeader(EOC3811, Dollcraft, 20)
CALL SpawnDollEvent
SilentGiveItem(EOC3811Doll, SoulLink)
ReplaceUnitIDEvent(EOC3811Doll, EOC3811DollSummoned)
GOTO 120
LABEL 20
EOCCooldownItemHeader(EOC3811, DollcraftPlus, 120)
CALL SpawnDollEvent
ReplaceUnitIDEvent(EOC3811Doll, EOC3811DollSummoned)
LABEL 120
NoFade
ENDA

SpawnDollEvent:
CAMERA EOC3811
LOAD1 0x1 EOC3811DollUnitPlacement
ENUN
MoveToSummonPositionEvent(EOC3811, EOC3811Doll)
MoveMemorySlot(sB, sC)
WARPIN [-1, -1]
ENDWARP
GetStrongestPlayerSummonEvent
MoveMemorySlot(s2, sC)
BEQ 120 sC s0
SVAL 1 EOC3811Doll
CopyStatsEventNoArgs
LABEL 120
NoFade
ENDA

BeginTurn5397:
EOC5397Phaser(EOC5397Phase1, EOC5397Phase2, UnfinishedMasterwork, 21, 120)
EOC5397Phaser(EOC5397Phase2, EOC5397Phase3, UnfinishedMasterwork, 22, 120)
EOC5397Phaser(EOC5397Phase3, EOC5397Phase4, UnfinishedMasterwork, 23, 120)
EOC5397Phaser(EOC5397Phase1, EOC5397Phase2, UnfinishedMasterworkPlus, 31, 120)
EOC5397Phaser(EOC5397Phase2, EOC5397Phase3, UnfinishedMasterworkPlus, 32, 120)
EOC5397Phaser(EOC5397Phase3, EOC5397Phase4, UnfinishedMasterworkPlus, 33, 120)
LABEL 120
BranchIfDead(EOC5397Phase4, 220)
SilentRemoveItemEvent(EOC5397Phase4, UnfinishedMasterworkPlus)
SilentRemoveItemEvent(EOC5397Phase4, UnfinishedMasterwork)
SilentRemoveItemEvent(EOC5397Phase4, MasterworkProgress)
LABEL 220
NoFade
ENDA

Promote5397Event: // Assumes old ID in s1, new one in s2
ReplaceUnitIDEventNoArgs
MoveMemorySlot(s1, s2) // Update old unit ID (s1) with new one (s2)
MoveMemorySlot(sC, s2)
ASMC ASMC_HealBadStatusLikeBoon|1 // HealBadStatusLikeBoon()
NoFade
ENDA

BeginTurn6048:
EOCTurnEventHeader(EOC6048, FogLevel, 120)
BranchIfMissingItem(EOC6048, EncroachingFog, 20)
CALL EncroachingFogEvent
GOTO 120
LABEL 20
BranchIfMissingItem(EOC6048, EncroachingFogPlus, 120)
CALL EncroachingFogEvent
CALL EncroachingFogEvent
LABEL 120
NoFade
ENDA

EncroachingFogEvent:
HasItemWithFullUsesEvent(EOC6048, FogLevel)
BNE 20 sC s0
RestoreItemUsesEvent(EOC6048, FogLevel, 1)
IncreaseFogLevelEvent
GOTO 120
LABEL 20
SilentRemoveItemEvent(EOC6048, EncroachingFog)
SilentRemoveItemEvent(EOC6048, EncroachingFogPlus)
LABEL 120
NoFade
ENDA

BeginTurn2136:
EOCTurnEventHeader(EOC2136, Incorporeal, 120)
BranchIfMissingItem(EOC2136, Regenerating, 120)
CALL EOC2136RestoreFormEvent
LABEL 120
NoFade
ENDA
